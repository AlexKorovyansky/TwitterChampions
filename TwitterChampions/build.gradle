import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://download.crashlytics.com/maven' }

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.7.+'
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'

    }
}
apply plugin: 'android'
apply plugin: 'crashlytics'

repositories {
    maven { url 'http://download.crashlytics.com/maven' }
    mavenCentral()

}

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

def gitSha() {
    return "git --git-dir=${rootDir}/../.git --work-tree=${rootDir}/.. rev-parse --short HEAD".execute().text.trim()
}

def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 19
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        buildConfigField 'String', 'GIT_SHA', '"' + "${gitSha()}" + '"'
        buildConfigField 'String', 'BUILD_TIME', '"' + "${buildTime()}'" + '"'
    }

    signingConfigs {
        debug {
            storeFile file("../distribution/debug.keystore")
        }
    }

    buildTypes {
        debug {
            packageNameSuffix '.debug'
            signingConfig signingConfigs.debug
        }
    }

    flavorGroups 'topic', 'buildType'

    productFlavors {

        AndroidDev {
            flavorGroup 'topic'
            buildConfigField 'String[]', 'HASH_TAGS', 'new String[] {"androiddev"}';
            buildConfigField 'String', 'SITE', '"https://github.com/korovyansk/TwitterChampions"';

            packageName 'com.alexkorovyansky.demo.twitterchampions'
        }

        DevFest2013 {
            flavorGroup 'topic'
            buildConfigField 'String[]', 'HASH_TAGS', 'new String[] {"devfest", "omsk"}';
            buildConfigField 'String', 'SITE', '"http://www.gdgomsk.org/gdg-devfest-2013"';

            packageName 'org.gdgomsk.devfest2013.twitterchampions'
        }

        _dev {
            flavorGroup 'buildType'
        }

        _production {
            flavorGroup 'buildType'
        }
    }
}

dependencies {
	compile 'com.android.support:appcompat-v7:18.0.0'

    compile 'com.squareup.dagger:dagger:1.0.1'
    compile 'com.squareup:otto:1.3.4'
    compile 'com.jakewharton.timber:timber:2.0.0'
    compile 'com.jakewharton:butterknife:3.0.0'
    compile 'com.squareup.retrofit:retrofit:1.2.2'
    compile 'com.squareup.picasso:picasso:2.1.1'

    compile 'com.crashlytics.android:crashlytics:1.+'
    compile 'com.google.code.gson:gson:2.2.4'

}
